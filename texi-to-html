#!/usr/bin/perl -W -*- cperl -*-

use strict;
use Digest::MD5 'md5_hex';

open TEXI, '<', 'doc/Tootsville.texi' or die;
`mkdir -p doc/Tootsville.html.d`;

my %files = ();
my $book_title = '';
my $current_chapter = 'forematter';
my $chapter_number = 0;
my $copying = '';

my %indices = ();

open NODE, '>', 'doc/Tootsville.html.d/forematter.html' or die;

my $node = 'forematter';

sub untexi ($) {
  $_ = shift;
  s(&) (&amp;)g;
  s(<) (&lt;)g;
  s(\@verb\{\|(.*?)\|\}) (<pre>$1</pre>)g; # FIXME
  s(\@code\{(.*?)\}) (<tt>$1</tt>)g;
  s(\@samp\{(.*?)\}) (<tt>$1</tt>)g;
  s(\@sup\{(.*?)\}) (<sup>$1</sup>)g;
  s(\@sub\{(.*?)\}) (<sub>$1</sub>)g;
  s(\@b\{(.*?)\}) (<b>$1</b>)g;
  s(\@i\{(.*?)\}) (<i>$1</i>)g;
  s(\@emph\{(.*?)\}) (<em>$1</em>)g;
  s(\@ref\{(.*?)\}) (my $ref = $1;
                     my $rname = $ref;
                     $rname =~ s(__)(/)g;
                     "<a href='$ref.html'>$rname</a>")ge;
  s(\@url\{(.*?)\}) (<a href="$1">$1</a>)g;
  s(\@copyright\{\}) (©)g;
  s/\@\@/\@/g;
  s(``(.*)'') (“$1”)g;
  s(`(.*)') (‘$1’)g;
  return $_;
}

my @nest = ();

{
  my $scrap =  <TEXI>;
}                               # discard first line

while (<TEXI>) {
  chomp;
  if (/\@verbatim/) {
    print NODE "<PRE>"; next;
  }
  if (/\@verbatim/../\@end verbatim/) {
    print NODE $_, "\n"; next;
  }
  if (/\@end verbatim/) {
    print NODE "</PRE>\n"; next;
  }

  next if (/^\s*\@c\s/);
  next if /\@setfilename/;
  next if /\@documentencoding UTF-8/;
  next if /\@setchapternewpage/;
  next if /\@macro/ .. /\@end macro/;
  next if /\@defindex/;
  next if /\@top/;

 # if (/\@copying/../\@end copying/) { $copying .= untexi($_) . "\n"; next; }
  if (/\@insertcopying/) { print NODE $copying; next }

  if (/\@settitle (.*)/) {
    $book_title = $1; next;
  }

  next if /\@ignore/;
  next if /\@end ignore/;

  next if /\@detailmenu/;
  next if /\@end detailmenu/;
  next if /\@ifnottex/;
  next if /\@end ifnottex/;
  
  if (/\@menu/) {
    print NODE "<nav><ul>\n"; next;
  }
  if (/^\* (.*):: *(.*)?/) {
    print NODE "<li> <a href='$1.html'>$1 $2</a></li>\n"; next
  }
  if (/\@end menu/) {
    print NODE "</ul></nav>\n"; next;
  }
  if (/\@table (.*)/) {
    print NODE "<dl>\n"; push @nest => 'table/' . $1; next;
  }
  if (/\@end table/) {
    unless ((pop @nest) =~ /^table\//) {
      die;
    }
    print NODE "</dl>\n"; next;
  }
  if (/\@itemize/) {
    print NODE "<ul>\n"; push @nest => 'itemize'; next;
  }
  if (/\@end itemize/) {
    unless ('itemize' eq (pop @nest)) {
      die;
    }
    print NODE "</ul>\n"; next;
  }
  if (/\@enumerate/) {
    print NODE "<ul>\n"; push @nest => 'enumerate'; next;
  }
  if (/\@end enumerate/) {
    unless ('enumerate' eq (pop @nest)) {
      die;
    }
    print NODE "</ul>\n"; next;
  }
  if (/\@item( +(.*))?/) {
    my $i = $2;
    if ($nest[-1] =~ /^table\/(.*)/) {
      my $op = $1;
      print NODE "<dt>", untexi($op."{$i}"), "</dt>\n<dd>"; next;
    } else {
      print NODE "<li>" ; next;
    }
  }


  if (/\@node (.*), (.*), (.*), (.*)/) {
    $node = $1;
    my ($next, $back, $top) = ($2, $3, $4);
    print NODE <<endfoot;
<hr>
<section id="footer">
On-line manual for Romance II • Tootsville V • Copyright © 2021, CIWTA
</section>
</body></html>
endfoot
    close NODE;
    my $title = $node;
    $node =~ s(/) (__)g;
    $node =~ s(\?) (%3f)g;
    open NODE, '>', "doc/Tootsville.html.d/$node.html" or die "can't write $node: $!";
    print NODE <<endhead;
<!DOCTYPE html>
<html>
<head><title> $title - $book_title </title>
<meta charset="utf-8">
<link rel="stylesheet" href="doc-style.css" type="text/css"></head>
<body>
<nav id="top"><ul>
endhead
    my ($backlink, $uplink, $nextlink) = ($back, $top, $next);
    $backlink =~ s(/) (__)g;
    $uplink =~ s(/) (__)g;
    $nextlink =~ s(/) (__)g;
    $backlink =~ s(\?) (%3f)g;
    $uplink =~ s(\?) (%3f)g;
    $nextlink =~ s(\?) (%3f)g;
    if ($backlink eq '(dir)') { $back = undef; }
    if ($uplink eq '(dir)') { $top = undef; }
    print NODE "<li id='backlink'><a href='$backlink.html'>$back</a></li>  " if $back;
    print NODE "<li id='uplink'><a href='$uplink.html'>$top</a></li>  " if $top;
    print NODE "<li id='nextlink'><a href='$nextlink.html'>$next</a></li>  " if $next;
    print NODE "</ul></nav> <hr>";
    ++$files{$node};
    next;
  }
  if (/\@node/) { die "Probably missing \@node markups, check TeXinfo file for navigation" }
  
  if (/\@printindex (..?)/) {
    my $index = $1;
    print NODE "<dl>\n";
    for my $key (sort keys %{$indices{$index}}) {
      print NODE "<dt> $key </dt> ";
      for my $ref (sort keys %{$indices{$index}{$key} }) {
        my $rname = $ref; $rname =~ s/__/\//g;
        print NODE "<dd><a href='$ref.html'>$rname</a> </dd>\n";
      }
    }
    print NODE "</dl>\n";
    next;
  }

  if (/\@example/ or /\@quotation/) {
    print NODE "<blockquote>\n";
    next;
  }
  if (/\@end example/ or /\@end quotation/) {
    print NODE "</blockquote>\n";
    next;
  }

  if (/\@chapter (.*)/) {
    $current_chapter = untexi $1;
    ++$chapter_number;
    print NODE "<h1> Chapter $chapter_number. $current_chapter </h1>\n";
    next;
  }

  if (/\@unnumbered (.*)/ or /\@appendix (.*)/) {
    $current_chapter = untexi $1;
    print NODE "<h1> $current_chapter </h1>\n";
    next;
  }


  if (/\@section (.*)/ or /\@appendixsec (.*)/) {
    print NODE "<h2>", untexi($1), "</h2>\n";
    next;
  }
  if (/\@subsection (.*)/ or /\@subheading (.*)/) {
    print NODE "<h3>", untexi($1), "</h3>\n";
    next;
  }
  if (/\@subsubsection (.*)/ or /\@subsubheading (.*)/) {
    print NODE "<h4>", untexi($1), "</h4>\n";
    next;
  }
  if (/\@page/) {
    print NODE "<hr>";
    next;
  }
  if (/\@(..?)index (.*)/) {
    my $i = $1;
    if ('f' eq $i) {
      $i = 'fn';
    }
    if ('c' eq $i) {
      $i = 'cp';
    }
    if ('v' eq $i) {
      $i = 'vr';
    }
    $indices{$i} ||= {};
    $indices{$i}{$2} ||= {};
    $indices{$i}{$2}{$node}++;
    next;
  }

  if (/^\s*$/) {
    print NODE "<p>"; next;
  }
  print NODE untexi($_) . "\n";
}

open INDEX, '>', 'doc/Tootsville.html.d/index.html' or die;

print INDEX <<index_end;
<!DOCTYPE html>
<html>
 <head>
  <title> Tootsville: On-line documentation </title>
  <link rel="stylesheet" href="doc-style.css" type="text/css">
 </head>
 <body>
  <h1> Tootsville: On-line documentation </h1>

<p> Copyright © 2008-2017 Bruce-Robert  Pocock. Copyright © 2018-2021 <a
href="https://ciwta.org/">The  Corporation for  Inter-World Tourism  and
Adventuring</a> </p>

<nav>  <a href="Top.html">Begin here.</a> </nav>
 </body>
</html>
index_end
